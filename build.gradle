buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'org.sonarqube.gradle:gradle-sonarqube-plugin:1.0'
    }
}

repositories {
    jcenter()
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

import org.gradle.internal.jvm.Jvm

group = 'droidkit'
version = file('VERSION').text.trim()

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compile files(Jvm.current().getToolsJar())
    compile 'com.squareup:javapoet:1.2.0'
    compile 'io.reactivex:rxjava:1.0.13'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publications = ['mavenJava']
    pkg {
        repo = 'droidkit'
        name = 'droidkit-processor'
        licenses = ['Apache-2.0']
    }
}